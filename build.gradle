plugins {
   id 'us.ihmc.gradle.ihmc-build' version '0.0.12'
   id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'us.ihmc.thirdparty.jme3' // add more to the path if you need
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0.1'

repositories ihmc.ihmcDefaultArtifactProxies()

repositories {
   maven {
      url 'http://updates.jmonkeyengine.org/maven'
   }
}

dependencies {
   compile group: 'com.jme3', name: 'jme3-core', version: '3.1.0-SNAPSHOT'
   compile group: 'com.jme3', name: 'jme3-desktop', version: '3.1.0-SNAPSHOT'
   compile group: 'com.vividsolutions', name: 'jts', version: '1.13'
}

jar {
   manifest {
      attributes(
              "Created-By": "IHMC Gradle Build Script")
   }
}

task sourceJar(type: Jar) {
   from sourceSets.main.allJava
}

publishing {
   publications {
      mavenJava(MavenPublication) {

         groupId 'us.ihmc.thirdparty.jme3'
         artifactId project.name
         version project.version

         from components.java

         artifact sourceJar {
            classifier "sources"
         }
      }
   }
}

bintray {
   user = project.hasProperty("bintray_user") ? project.bintray_user : "invalid"
   key = project.hasProperty("bintray_key") ? project.bintray_key : "invalid"

   if (user.equals("invalid")) {
      project.logger.debug("Bintray user name property not set. Please set the 'bintray_user' property in ~/.gradle/gradle.properties. See https://github.com/bintray/gradle-bintray-plugin")
   }

   if (key.equals("invalid")) {
      project.logger.debug("Bintray API key property not set. Please set the 'bintray_key' property in ~/.gradle/gradle.properties. See https://github.com/bintray/gradle-bintray-plugin")
   }

   dryRun = false
   publish = false

   publications = ["mavenJava"]

   pkg {
      repo = "maven-vendor"
      userOrg = "ihmcrobotics"
      name = "jme3-shape3d"
      desc = "Maven artifact providing jMonkeyEngine Shape3D community contributed plugin"

      vcsUrl = "https://github.com/ihmcrobotics/jme3-shape3d.git"

      licenses = ['MIT']
      labels = ['java', 'jmonkeyengine', 'plugin', 'vendored']

      version {
         name = project.version
         released = new Date()
      }
   }
}